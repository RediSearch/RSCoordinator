CMAKE_MINIMUM_REQUIRED(VERSION 2.8.9)
PROJECT(RSCoordinator)

ADD_DEFINITIONS(-DREDISMODULE_EXPERIMENTAL_API)
ADD_DEFINITIONS(-D_GNU_SOURCE)
ADD_DEFINITIONS(-DREDIS_MODULE_TARGET)
ADD_DEFINITIONS(-DRS_CMD_PREFIX="_FT")
ADD_DEFINITIONS(-DRS_COORDINATOR="ON")
ADD_DEFINITIONS( -UNDEBUG )
# Also remove /D NDEBUG to avoid MSVC warnings about conflicting defines.
FOREACH (flags_var_to_scrub
    CMAKE_CXX_FLAGS_RELEASE
    CMAKE_CXX_FLAGS_RELWITHDEBINFO
    CMAKE_CXX_FLAGS_MINSIZEREL
    CMAKE_C_FLAGS_RELEASE
    CMAKE_C_FLAGS_RELWITHDEBINFO
    CMAKE_C_FLAGS_MINSIZEREL)
  STRING(REGEX REPLACE "(^| )[/-]D *NDEBUG($| )" " "
    "${flags_var_to_scrub}" "${${flags_var_to_scrub}}")
ENDFOREACH()


SET(RS_COMMON_FLAGS "-Wall -Wno-unused-function -Wno-unused-variable -Wno-unused-result")
SET(RS_COMMON_FLAGS "${RS_COMMON_FLAGS} -fPIC -Werror=implicit-function-declaration")
SET(RS_COMMON_FLAGS "${RS_COMMON_FLAGS} -pthread")
# Specific to coordinator
SET(RS_COMMON_FLAGS "${RS_COMMON_FLAGS} -fvisibility=hidden")

SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${RS_COMMON_FLAGS} -std=gnu99")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${RS_COMMON_FLAGS} -fno-rtti -fno-exceptions")

SET(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -Bsymbolic -Bsymbolic-functions")
SET(CMAKE_MODULE_LINKER_FLAGS "${CMAKE_MODULE_LINKER_FLAGS} -Bsymbolic -Bsymbolic-functions")

SET(RS_DIR ${PROJECT_SOURCE_DIR}/src/dep/RediSearch)
SET(RS_RUN_TESTS CACHE BOOL OFF) # Don't run RS' own tests
ENABLE_TESTING() # Enable our own tests

FIND_PATH(LIBUV_INCLUDE_DIR uv.h
          HINTS ${LIBUV_ROOT}
          PATH_SUFFIXES include)

FIND_LIBRARY(LIBUV_LIBRARIES
    NAMES libuv.a uv
    HINTS ${LIBUV_ROOT}
    PATH_SUFFIXES lib)

INCLUDE_DIRECTORIES(${RS_DIR}/src)
INCLUDE_DIRECTORIES(${LIBUV_INCLUDE_DIR})

ADD_SUBDIRECTORY(src/dep/RediSearch)
ADD_SUBDIRECTORY(src/dep/rmr)
ADD_SUBDIRECTORY(src/dep/rmutil)
ADD_SUBDIRECTORY(src)

SET(FINAL_OBJECTS
    ${COORDINATOR_SRC}
    $<TARGET_OBJECTS:coordinator-core>
    $<TARGET_OBJECTS:coordinator-rmutil>
    $<TARGET_OBJECTS:rmr>)

MACRO(MODULE_TARGET SUFFIX)
    SET(_tgtName module-${SUFFIX})
    ADD_LIBRARY(${_tgtName} MODULE ${FINAL_OBJECTS} src/build-info/info-${SUFFIX}.c)
    TARGET_LINK_LIBRARIES(${_tgtName} redisearchS ${LIBUV_LIBRARIES} hiredis)
    SET_TARGET_PROPERTIES(${_tgtName} PROPERTIES PREFIX "")
    SET_TARGET_PROPERTIES(${_tgtName} PROPERTIES SUFFIX ".so")
    IF (APPLE)
        SET_TARGET_PROPERTIES(${_tgtName} PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
    ENDIF()
ENDMACRO()

MODULE_TARGET(oss)
MODULE_TARGET(enterprise)

ADD_LIBRARY(testdeps STATIC ${FINAL_OBJECTS})
TARGET_LINK_LIBRARIES(testdeps ${LIBUV_LIBRARIES} redisearchS hiredis apistubs)
# Add RMR's tests, and our own tests
ADD_SUBDIRECTORY(src/dep/rmr/test)
ADD_SUBDIRECTORY(test)
